alias Maru.Route

defmodule Route do
  defstruct [
    method:     nil,
    path:       [],
    version:    nil,
    parameters: [],
    helpers:    [],
    plugs:      [],
    module:     nil,
    func_id:    nil,
  ]
  ++ Maru.Builder.Exception.route_struct()
  ++ Maru.Builder.Description.route_struct()


  defmacro __using__(_) do
    quote do
      Module.register_attribute __MODULE__, :routes,  accumulate: true
      @func_id 0

      use Route.Mount
      use Route.Endpoint
    end
  end

  def before_parse_namespace(env) do
    Maru.Builder.Pipeline.before_parse_namespace(env)
    Maru.Builder.Parameter.before_parse_namespace(env)
  end

  def before_parse_method(%Macro.Env{module: module}=env) do
    func_id = Module.get_attribute(module, :func_id)
    Module.put_attribute(module, :func_id, func_id + 1)

    context = Module.get_attribute(module, :method_context)
    Module.put_attribute(module, :method_context, Map.put(context, :func_id, func_id))

    route =
      context
      |> Map.take([:method, :version, :path, :helpers, :plugs])
      |> Map.merge(%{module: module, func_id: func_id})
    Module.put_attribute(module, :route, struct(Route, route))

    Maru.Builder.Parameter.before_parse_route(env)
    Maru.Builder.Description.before_parse_route(env)

    route = Module.get_attribute(module, :route)
    Module.put_attribute(module, :routes, route)

    Route.Endpoint.before_parse_method(env)
  end

  def before_compile_router(%Macro.Env{module: module}=env) do
    current_routes = Module.get_attribute(module, :routes)  |> Enum.reverse
    mounted_routes = Module.get_attribute(module, :mounted) |> Enum.reverse
    extend_opts    = Module.get_attribute(module, :extend)
    extended       = Route.Extend.take_extended(
      current_routes ++ mounted_routes, extend_opts
    )
    all_routes     = current_routes ++ mounted_routes ++ extended
    Module.put_attribute(module, :all_routes, all_routes)

    quoted = quote do
      def __routes__, do: unquote(Macro.escape(all_routes))
    end
    Module.eval_quoted(env, quoted)

    Route.Endpoint.before_compile_router(env)
  end
end
